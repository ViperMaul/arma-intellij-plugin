{
  parserClass="com.kaylerrenslow.armaplugin.lang.sqf.parser.SQFParser"
  parserUtilClass="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"


  // Question: Why do we start with SQFPsi? Because we have our own custom interfaces like
  // SQFCommand that the plugin uses instead of directly referencing generated psi elements used in the AST.
  psiClassPrefix="SQFPsi"
  psiImplClassSuffix="Impl"
  psiPackage="com.kaylerrenslow.armaplugin.lang.sqf.psi"
  psiImplPackage="com.kaylerrenslow.armaplugin.lang.sqf.psi.impl"

  elementTypeHolderClass="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFTypes"
  elementTypeClass="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFElementType"
  tokenTypeClass="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFTokenType"

//  psiImplUtilClass="com.kaylerrenslow.armapluginin.lang.sqf.psi.SQFPsiImplUtilForGrammar"

  extends(".*expression")=expression
  //consumeTokenMethod(".*expression|expression")="consumeTokenFast" //https://github.com/JetBrains/Grammar-Kit/blob/master/HOWTO.md#24-compact-expression-parsing-with-priorities
  extends(".*_statement")=statement
}
//Question: Why do some rules end with _? Answer: It is just to make it clear that they are private rules.


//todo use stubs. check StubBasedPsiElementBase class for more information on it

// @formatter:off
sqfFile ::= file_scope //first rule is private. place file_scope inside and file_scope will be root rule

//file_scope is the scope for the entire SQF file. There will be only 1 file_scope per sqf file
file_scope ::= items_ {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFFileScope"}
//local_scope is a scope inside a code block
local_scope ::= items_  {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFLocalScope"}
//items_ is a helper for file_scope and local_scope
private items_ ::= (statement SEMICOLON)* statement?

code_block ::= L_CURLY_BRACE local_scope? R_CURLY_BRACE {pin=1 mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCodeBlock"}

//a variable is a global variable or local variable
variable ::= GLOBAL_VAR | LOCAL_VAR {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFVariable"}

//* a statement is a line of code that has a semicolon after it, however, if there is 1 statement, then semicolon is optional
//* case statement must come before expression or case statement will never be discovered because of command expression
statement ::= ( assignment_statement | case_statement | expression_statement | quest_statement )
//a return statement is a statement that has NO semicolon and is the last statement in a scope
//return_statement ::= variable | ( assignment_statement | case_statement | expression | quest_statement )

quest_statement ::= QUEST expression COLON expression {pin=1 mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFQuestStatement"}
case_statement ::= case_command expression (COLON code_block)? {pin=1 mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCaseStatement"}

//in SQF, it isn't possible to do a=a=a=a, so making it an expression will create issues
assignment_statement ::= private_command? variable
                        !(command /*make sure not to expect part of a command expression*/)
                        EQ expression {pin=4 mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFAssignmentStatement"}
expression_statement::=expression  {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFExpressionStatement"}
//expression
    expression ::= conditional_group_
                | add_group_
                | comp_expression
                | mul_group_
                | exponent_expression
                | config_fetch_expression

                | command_expression

                 // keep below command expression, otherwise the command may not be
                 // matched since a command can have a unary expression as an argument
                | signed_expression

                | literal_expression
                | code_block_expression
                | paren_expression

    code_block_expression ::= code_block {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCodeBlockExpression"}

    private add_group_ ::= add_expression | sub_expression
    private mul_group_ ::= mul_expression | div_expression | mod_expression
    private conditional_group_::= bool_and_expression | bool_or_expression | bool_not_expression

    signed_expression ::= (PLUS | MINUS) expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFSignedExpression"}

    add_expression ::= expression PLUS expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFAddExpression"}
    sub_expression ::= expression MINUS expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFSubExpression"}
    mul_expression ::= expression ASTERISK expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFMultExpression"}
    div_expression ::= expression FSLASH expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFDivExpression"}
    mod_expression ::= expression PERC expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFModExpression"}

    bool_and_expression ::= expression AMPAMP expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFBoolAndExpression"}
    bool_or_expression ::= expression BARBAR expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFBoolOrExpression"}
    bool_not_expression::= EXCL expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFBoolNotExpression"}

    comp_expression ::= expression (EQEQ | NE | LT | LE | GT | GE) expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCompExpression"}

    config_fetch_expression ::= expression (FSLASH | GTGT) expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFConfigFetchExpression"}

    exponent_expression ::= expression CARET expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFExponentExpression"}

    literal_expression ::= string
                        | variable
                        | array
                        | number {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFLiteralExpression"}

    paren_expression ::= LPAREN expression RPAREN {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFParenExpression"}

    //command expression (includes a standalone command token)
        command_expression ::= command_before? command command_after? {pin=2 mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCommandExpression"}
        //We are excluding expression from this because it will be infinite left recursion
        //due to command_expression constantly being matched.

        //The prefix and postfix arguments of commands is inconsistent.
        //The type checker will have to handle if command_before or command_after is a prefix argument,
        //postfix argument, or is an argument of the child/parent command. (I know. It's weird.)
        command_before ::= code_block_expression | signed_expression | paren_expression | literal_expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCommandArgument"}

        //for the some like command _var == 0, we want the command expression to consume _var and then be wrapped in a boolean comparison node. (COMMAND_EXPR==0 rather than COMMAND_EXPR BOOL_COMP_EXPR)
        command_after ::= literal_expression | expression {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCommandArgument"}
    //end command expression
//end expression

//command tokens
    command ::= COMMAND_TOKEN {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFCommand"}
    //we need a separate rule for case and private
    //the "case" and "private" tokens are COMMAND_TOKEN types, but we want custom types (i.e. CASE_COMMAND)
    //so that we can check if (for example) "case" is the COMMAND_TOKEN and not any arbitrary COMMAND_TOKEN (like abs)
    case_command ::= <<external_rule_case_command 0>> {extends("case_command")=command}
    private_command ::= <<external_rule_private_command 0>> {extends("private_command")=command}
//end command

//values
string ::= STRING_LITERAL {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFString"}
array ::= L_SQ_BRACKET (expression (COMMA expression)*)? R_SQ_BRACKET {pin=1 mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFArray"}
number ::= INTEGER_LITERAL | DEC_LITERAL | HEX_LITERAL {mixin="com.kaylerrenslow.armaplugin.lang.sqf.psi.SQFNumber"}