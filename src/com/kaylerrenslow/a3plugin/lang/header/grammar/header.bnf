{
  parserClass="com.kaylerrenslow.a3plugin.lang.header.parser.HeaderParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Header"
  psiImplClassSuffix="Impl"
  psiPackage="com.kaylerrenslow.a3plugin.lang.header.psi"
  psiImplPackage="com.kaylerrenslow.a3plugin.lang.header.psi.impl" //comment out the import statement in the HeaderTypes class if the impl package isn't used

  elementTypeHolderClass="com.kaylerrenslow.a3plugin.lang.header.psi.HeaderTypes"
  elementTypeClass="com.kaylerrenslow.a3plugin.lang.header.psi.HeaderElementType"
  tokenTypeClass="com.kaylerrenslow.a3plugin.lang.header.psi.HeaderTokenType"

  psiImplUtilClass="com.kaylerrenslow.a3plugin.lang.header.psi.impl.PSIHeaderImplUtil"
}
// @formatter:off
headerFile ::= item_*

//TODO when $ is typed, look into stringtable.xml (try using annotator)
//TODO add in \ lexer rule that is a waiting value just like in the properties custom language demo. This will work for any assignment
//TODO maybe use annotator for SQF config retrieval (config >> "var"). check if the PsiFile is over HeaderFile so you can't annotate in any other language
private item_ ::= COMMENT | BLOCK_COMMENT | statement | preprocessor | WHITE_SPACE | BAD_CHARACTER

statement ::= (assignment | class_declaration) SEMICOLON

number ::= NUMBER_LITERAL | HEX_LITERAL
value ::= STRING_LITERAL | number | CONSTANT | IDENTIFIER

array ::= LBRACE RBRACE | LBRACE (expression | value | array)(COMMA (expression | value | array))* RBRACE

assignment_identifier ::= IDENTIFIER /*| NUM_IDENTIFIER*/

assignment ::= basic_assignment | array_assignment
basic_assignment ::= assignment_identifier EQ (expression | value )
array_assignment ::= assignment_identifier BRACKET_PAIR EQ array

expression ::= add_expression_

private add_expression_ ::= mult_expression_ PLUS add_expression_
				 | mult_expression_ MINUS add_expression_
				 | mult_expression_

private mult_expression_ ::= term_ ASTERISK mult_expression_
				  | term_ FSLASH mult_expression_
				  | term_ PERC mult_expression_
				  | term_ MOD mult_expression_
				  | term_ CARET mult_expression_
				  | term_

private term_ ::= (PLUS | MINUS)? (
			number
            | IDENTIFIER
			| LPAREN expression RPAREN
		  )


class_declaration ::= CLASS IDENTIFIER (COLON IDENTIFIER)? (LBRACE class_content RBRACE)?

class_content ::= item_*

//Preprocessor things
preprocessor ::= pre_include //| pre_define_//TODO

pre_include ::= PREPROCESS_INCLUDE (STRING_LITERAL | INCLUDE_VALUE_ANGBR) //TODO does include or other preprocessor commands allow multilines? (https://community.bistudio.com/wiki/PreProcessor_Commands#Multi-line
//private pre_define_ ::= PREPROCESS_DEFINE