{
  parserClass="com.kaylerrenslow.a3plugin.lang.header.parser.HeaderParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Header"
  psiImplClassSuffix="Impl"
  psiPackage="com.kaylerrenslow.a3plugin.lang.header.psi"
  psiImplPackage="com.kaylerrenslow.a3plugin.lang.header.psi.impl" //comment out the import statement in the HeaderTypes class if the impl package isn't used

  elementTypeHolderClass="com.kaylerrenslow.a3plugin.lang.header.psi.HeaderTypes"
  elementTypeClass="com.kaylerrenslow.a3plugin.lang.header.psi.HeaderElementType"
  tokenTypeClass="com.kaylerrenslow.a3plugin.lang.header.psi.HeaderTokenType"

  psiImplUtilClass="com.kaylerrenslow.a3plugin.lang.header.psi.helpers.PSIHeaderImplUtil"
}
// @formatter:off
headerFile ::= item_*

//TODO when $ is typed, look into stringtable.xml (try using annotator)
//TODO add in \ lexer rule that is a waiting value just like in the properties custom language demo. This will work for any assignment
//TODO maybe use annotator for SQF config retrieval (config >> "var"). check if the PsiFile is over HeaderFile so you can't annotate in any other language
private item_ ::= COMMENT | BLOCK_COMMENT | statement | preprocessor | WHITE_SPACE | BAD_CHARACTER

statement ::= (assignment | class_declaration) SEMICOLON

number ::= NUMBER_LITERAL | HEX_LITERAL
value ::= STRING_LITERAL | number | CONSTANT | IDENTIFIER

array ::= LBRACE RBRACE | LBRACE (expression | value | array)(COMMA (expression | value | array))* RBRACE

assignment_identifier ::= IDENTIFIER /*| NUM_IDENTIFIER*/

assignment ::= basic_assignment | array_assignment
basic_assignment ::= assignment_identifier EQ (expression | value )
array_assignment ::= assignment_identifier BRACKET_PAIR EQ array

expression ::= add_expression

add_expression ::= mult_expression PLUS add_expression
				 | mult_expression MINUS add_expression
				 | mult_expression

mult_expression ::= term ASTERISK mult_expression
				  | term FSLASH mult_expression
				  | term PERC mult_expression
				  | term MOD mult_expression
				  | term CARET mult_expression
				  | term

term ::= (PLUS | MINUS)? (
			number
            | IDENTIFIER
			| LPAREN expression RPAREN
		  )


class_declaration ::= CLASS IDENTIFIER (COLON IDENTIFIER)? class_content?

class_content ::= LBRACE (item_)* RBRACE

//Preprocessor things
preprocessor ::= pre_include //| pre_define_//TODO

pre_include ::= PREPROCESS_INCLUDE (STRING_LITERAL | INCLUDE_VALUE_ANGBR) //TODO does include or other preprocessor commands allow multilines? (https://community.bistudio.com/wiki/PreProcessor_Commands#Multi-line
//private pre_define_ ::= PREPROCESS_DEFINE